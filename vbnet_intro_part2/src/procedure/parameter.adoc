= {% autotitle %}{% endautotitle %}
:icons: font

== What happens when I change the value of a parameter inside a Function or Sub?

Unless you specify otherwise (see the section below), the arguments are passed *by value*.
It means copies of the variables are passed into the procedure.
If any parameters is changed in the procedure, only the copy is changed.
The original value is not changed with the copy.

[NOTE]
====
Changing the value of an argument passed by value inside a procedure is bad for readability.
Do this with care.
====


== Appendix: Passing arguments by reference

To allow procedures to change the value of the original argument, you can set the parameter to pass *by reference*.
In VB.NET, this is done by adding the `ByRef` keyword before the parameter.

Passing parameters by reference is used mainly

* to allow the use of more than one output value in a function.
* to avoid the performance penalty of copying a large data structure.

However, with the internal use of *reference types* in VB.NET, we usually do not incur the performance penalty by passing “by value”.


<<<
== Example: ByRef keyword

[source, vb]
....
Module Module1
    ' x is passed to PassByVal by value.
    Sub PassByVal(x As Integer)
        x = 10
    End Sub

    ' x is passed to PassByVal by reference.
    Sub PassByRef(ByRef x As Integer)
        x = 10
    End Sub

    Sub Main()
        Dim value As Integer = 1

        ' The value doesn't change here when passed by value.
        PassByVal(value)
        Console.WriteLine("After PassByVal: {0}", value)

        ' The value DOES change when passed ByRef.
        PassByRef(value)
        Console.WriteLine("After PassByRef: {0}", value)

        Console.ReadLine()
    End Sub
End Module
....

[role="sample-output", subs="normal"]
....
After PassByVal: 1
After PassByRef: 10
....


<<<
== Example: using a sub to swap numbers

[source, vb]
....
Module Module1
    Sub SwapIntegers(ByRef first As Integer,
                     ByRef second As Integer)
        Dim tempValue As Integer = first
        first = second
        second = tempValue
    End Sub

    Sub Main()
        Dim x As Integer = 3
        Dim y As Integer = 4
        Console.WriteLine("Before swap: x = {0}, y = {1}", x, y)

        SwapIntegers(x, y)
        Console.WriteLine("After swap: x = {0}, y = {1}", x, y)

        Console.ReadLine()
    End Sub
End Module
....

[role="sample-output", subs="normal"]
....
Before swap: x = 3, y = 4
After swap: x = 4, y = 3
....

== Appendix: Value type and reference type in .NET

Some data types are stored internally as a reference to some underlying data, e.g. `String`.
These are known as *reference types* in .NET platform.
In contrast, `Integer`, `Single`, `Double` and `Boolean` are *value types*.

This has some implications on passing arguments in procedures, which are discussed in
https://msdn.microsoft.com/en-us/library/eek064h4.aspx
and http://net-informations.com/faq/general/valuetype-referencetype.htm.
