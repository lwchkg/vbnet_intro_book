= {% autotitle %}{% endautotitle %}
:icons: font

In this section, we discuss the details of functions `Len`, `Left`, `Right`, `Mid`, `Trim`, `AscW` and `ChrW`.
String matching will be discussed in another section.

In some string functions, there are *boundary cases* that need to be discussed.
A boundary case is a special case that one or more input is at or just beyond its maximum or minimum limits.
For example, if the valid mark is from 0 to 100, we consider -1, -0.1, 0, 100, 100.1 and 101 as the boundary cases.

[CAUTION]
====
If you intend to write a real application, use string functions that work properly in Unicode.
In the functions discussed here, only `UCase` and `LCase` work properly.
====


== Len function

`Len` function means to return the number of characters in the string.
An empty string (`""`) has a length of 0.
Here is an example:

[source, vb]
....
Dim TestString As String = "Hello World"
Dim TestLength As Integer = Len(TestString)  ' Returns 11.
....


<<<
== Left and Right functions

`Left` and `Right` extract the specified number of characters from the left and right of the given string respectively.
Some special cases are described here:

[options="header"]
|===
|Condition |Left / Right returns
|Length ≥ length of string |Returns the whole string.
|Length = 0    |Returns an empty string (`""`).
|Length < 0 |*Exception*. Results in runtime error if not handled.
|===

[TIP]
====
It does not make sense to pass a negative length to string functions.
However, if you pass a variable as the length, you need to check for unexpected cases.
====

[NOTE]
====
Exception handling is not included in this book.
Anyway, exceptions in string functions are not meant to be handled.
====


Here is an example:
[source, vb]
....
Dim TestString As String = "Hello World!"
Dim result As String

result = Left(TestString, 5)    ' Returns "Hello".
result = Left(TestString, 100)  ' Returns "Hello World!".
result = Left(TestString, 0)    ' Returns "".

result = Right(TestString, 5)    ' Returns "orld!".
result = Right(TestString, 100)  ' Returns "Hello World!".
result = Right(TestString, 0)    ' Returns "".
....


<<<
== Mid function

`Mid` function extracts the specified number of characters beginning from the given starting position.
The position is one-based, i.e. the first character of a string has a position of 1.
Some special cases are described here:

[options="header"]
|===
|Condition |Mid returns
|Length is not specified |Returns everything starting from the given position.
|Start = 1 |Return is same as `Left`.
|Start > length of string    |Returns an empty string (`""`).
|Start ≤ 0 or Length < 0 |Exception. Results in runtime error if not handled.
|===


Here is an example:
[source, vb]
....
Dim TestString As String = "Mid Function Demo"
Dim result As String

result = Mid(TestString, 1, 3)  ' Returns "Mid".
result = Mid(TestString, 14, 4)  ' Returns "Demo".
result = Mid(TestString, 5)  ' Returns "Function Demo".
....


<<<
== Trim function

`Trim` function removes white spaces in the beginning and the end of the string.
This is used for sanitising user inputs because users may add unneeded spaces to the input.

The spaces at the middle of the strings are *not* removed.

However, we should note that “white space” in this function also includes ideographic space (code point 12288), which is commonly used in CJK (Chinese, Japanese and Korean) text.


[source, vb]
....
Dim TestString As String = "  Visual Basic  "

' Returns "Visual Basic".
Dim result As String = Trim(TestString)
....


[TIP]
====
If you want to remove white spaces at the beginning of the string only, use `LTrim`.
For the end of the string, use `RTrim`.
====


== UCase and LCase functions

UCase and LCase functions convert all letters in the strings to upper case and lower case respectively.
Other characters, such as digits, are not affected.

[NOTE]
====
In addition to “a” to “z”, a lot of characters have an upper case or a lower case variant.
For example, the upper case variant of the symbol “π” is “Π”.
====

[source, vb]
....
Dim TestString As String = "Hello World 1234!"
Dim result As String

result = UCase(TestString)  ' Returns "HELLO WORLD 1234!".
result = LCase(TestString)  ' Returns "hello world 1234!".
....

[WARNING]
====
Beware of `UCase("i")` and `LCase("I")`.
In Turkish, they evaluate to `"İ"` and `"ı"` respectively.
====


<<<
== AscW and ChrW functions

`AscW` returns the Unicode code point of the first character of the string.
And `ChrW` returns a character corresponding to the Unicode code point.
However, these functions work only if the character has a Unicode code point of 65535 or below.

[source, vb]
....
Dim code As Integer
code = AscW("A")  ' Returns 65.
code = AscW("Apple")  ' Returns 65.
code = AscW("a")  ' Returns 97.
code = AscW("0")  ' Returns 48.
code = AscW(vbCrLf)  ' Returns 13.

Dim character As String
character = ChrW(65)  ' Returns "A".
character = ChrW(97)  ' Returns "a".
character = ChrW(51)  ' Returns "3".
character = ChrW(33)  ' Returns "!".
....


== Class Work

Evaluate the following VB.NET expressions.
In this exercise, `UCase` and `LCase` are executed in American English.

[options="header",cols="2l, 1l"]
|===
|VB.NET expression |Result
|Right("S.2", 1) & Left("Cactus", 1) |
|UCase(LCase("TeStInG")) |
|Len(Mid(Trim("  very good "), 7, 4)) |
|AscW("E") |
|ChrW(AscW("Z") + 20) |
|AscW(UCase("basic")) |
|AscW(Mid(vbCrLf, 2)) |
|===


<<<
== Integrated example: ROT13 cipher

Here we implement a toy cipher called “ROT13” (rotate characters by 13 places).
While ROT13 cannot safely keep secrets, it can be used to hide spoilers.

[source, vb]
....
Module Module1
    Function RotateCharBy13(ch As String) As String
        If ch >= "A" And ch <= "Z" Then
            Return ChrW((AscW(ch) - 65 + 13) Mod 26 + 65)
        End If
        If ch >= "a" And ch <= "z" Then
            Return ChrW((AscW(ch) - 97 + 13) Mod 26 + 97)
        End If
        Return ch
    End Function

    Sub Main()
        Console.WriteLine("ROT13 - Rotate by 13 places")
        Console.WriteLine("Enter the text to process:")
        Dim s As String = Console.ReadLine()

        Console.WriteLine()
        Console.WriteLine("Processed text:")

        Dim length = Len(s)
        Dim pos = 1
        Do Until pos > length
            Console.Write(RotateCharBy13(Mid(s, pos, 1)))
            pos += 1
        Loop
        Console.WriteLine()

        Console.ReadLine()
    End Sub
End Module
....

<<<

Now see the cipher into action.
The second ROT13 operation cancels the first.

[role="sample-output", subs="normal"]
....
ROT13 - Rotate by 13 places
===========================
Enter the text to process:
[userinput]#Visual Basic is awesome!#

Processed text:
Ivfhny Onfvp vf njrfbzr!
....

[role="sample-output", subs="normal"]
....
ROT13 - Rotate by 13 places
===========================
Enter the text to process:
[userinput]#Ivfhny Onfvp vf njrfbzr!#

Processed text:
Visual Basic is awesome!
....


[NOTE]
====
`RotateCharBy13` in this example is not a robust implementation because the behavior is undefined if multiple characters are passed to `ch`.
However, the fix of this problem is too complicated to be discussed in this book.
====
